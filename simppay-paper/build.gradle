import org.glavo.rcon.Rcon

plugins {
    id 'java'
    id 'com.gradleup.shadow'
}
version = getRootProject().version
dependencies {
    // Include the common module
    implementation project(':simppay-converter')

    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    compileOnly 'com.h2database:h2:2.3.232'
    compileOnly "io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT"
    compileOnly 'com.j256.ormlite:ormlite-jdbc:6.1'
    compileOnly 'xyz.xenondevs.invui:invui:1.49'
    compileOnly 'org.black_ixx:playerpoints:3.3.0'

    compileOnly('me.clip:placeholderapi:2.11.6') {
        exclude group: 'me.clip.placeholderapi', module: 'libs'
    }
    compileOnly('org.geysermc.floodgate:api:2.2.4-SNAPSHOT')
    compileOnly("su.nightexpress.coinsengine:CoinsEngine:2.5.0")
    compileOnly("com.squareup.okhttp3:okhttp:5.0.0-alpha.12")
    implementation("de.exlll:configlib-paper:4.8.0") {
        exclude group: 'org.yaml', module: 'snakeyaml-engine'
    }
    implementation "dev.jorel:commandapi-paper-shade:11.1.0"
    implementation("com.tcoded:FoliaLib:0.5.1")
    implementation("com.github.retrooper:packetevents-spigot:2.11.1") {
        exclude group: 'net.kyori'
    }
    implementation('net.wesjd:anvilgui:1.10.6-SNAPSHOT')
}

shadowJar {
    // Configure name & output location
    archiveBaseName.set("SimpPay")
    archiveVersion.set(version)
    archiveClassifier.set('')
    if (System.getenv("OUTPUT_DIR") != null) {
        destinationDirectory.set(file(System.getenv("OUTPUT_DIR")))
    } else {
        destinationDirectory.set(file("$projectDir/../build/libs"))
    }
    relocate("de.exlll.configlib", "me.typical.lib.configlib")
    relocate("dev.jorel.commandapi", "me.typical.lib.commandapi")
    relocate("com.tcoded.folialib", "me.typical.lib.folialib")
    relocate("org.json.json", "me.typical.lib.json")
    relocate("io.github.retrooper.packetevents", "me.typical.lib.io.packetevents")
    relocate("com.github.retrooper.packetevents", "me.typical.lib.com.packetevents")
    relocate("net.wesjd.anvilgui", "me.typical.lib.anvilgui")
}
processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
tasks.build {
    dependsOn(shadowJar)
}
