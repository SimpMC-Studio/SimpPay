buildscript {
    ext {
        joobyVersion = "3.7.0"
    }
}

plugins {
    id 'java'
    id 'application'
    id "io.jooby.run" version "${joobyVersion}"
    id "com.gradleup.shadow" version "8.3.5"
    id "io.spring.dependency-management" version "1.1.7"
}

group = getRootProject().group
version = getRootProject().version
mainClassName = "org.simpmc.simppay.http.JoobyServer"

dependencyManagement {
    imports {
        mavenBom "io.jooby:jooby-bom:$joobyVersion"
    }
}

dependencies {
    // Include the common module
    implementation project(':simppay-api')
    
    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    
    // Database
    implementation 'com.j256.ormlite:ormlite-jdbc:6.1'
    implementation 'com.zaxxer:HikariCP:6.3.0'
    implementation 'com.h2database:h2:2.3.232'
    
    // Jooby framework
    implementation "io.jooby:jooby-netty"
    implementation "io.jooby:jooby-hikari"
    implementation "io.jooby:jooby-redis"
    implementation "io.jooby:jooby-logback"
    annotationProcessor "io.jooby:jooby-apt"

    // Testing
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.11.4"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.11.4"
    testImplementation "io.jooby:jooby-test"
    testImplementation "com.squareup.okhttp3:okhttp:4.12.0"
}

test {
    useJUnitPlatform()
}

/** Java debug information: */
tasks.withType(JavaCompile) {
    options.compilerArgs += [
            '-parameters',
            '-Ajooby.incremental=true',
            '-Ajooby.services=true',
            '-Ajooby.debug=false'
    ]
    options.debug = true
}

shadowJar {
    mergeServiceFiles()
}

joobyRun {
    mainClass = mainClassName
    restartExtensions = ["conf", "properties", "class"]
    compileExtensions = ["java"]
    port = 8080
}
